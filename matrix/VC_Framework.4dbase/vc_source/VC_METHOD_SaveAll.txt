//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): jfletcher
// Date and time: 04/05/12, 15:29:48
// ----------------------------------------------------
// Description:
//   Wrapper for VC_METHOD_Save, loop over all
//   unsaved methods and save.
//
//   Warning: paths should be validated before calling
//   this method (see VC_METHOD_ValidateAll)
// --[HEADEREND]---------------------------------------
C_POINTER($1;$methodsToSave_p_at)

C_LONGINT($0;$error_l)

C_LONGINT($numMethods_l;$currentMethod_l;$progID_l;$progThreshold_l)
C_TEXT($currentMethodPath_t;$progTitle_t)
C_REAL($progress_r)

$methodsToSave_p_at:=$1

$numMethods_l:=Size of array($methodsToSave_p_at->)

If ($numMethods_l>0)

$currentMethod_l:=1

$progThreshold_l:=1000// Only show progress bar if this will take longer than 1 second.
$progTitle_t:=<>VC_ComponentName_t+" Processing Methods"
$progID_l:=PROG_Create ($progTitle_t;$numMethods_l;$progThreshold_l)

// Loop over each method to save...
While (($currentMethod_l<=$numMethods_l) & ($error_l=0))

$currentMethodPath_t:=$methodsToSave_p_at->{$currentMethod_l}
$progress_r:=$currentMethod_l/$numMethods_l

PROG_Update ($progID_l;$progress_r;$currentMethod_l)

// Save the method.
If ($error_l=0)
$error_l:=VC_METHOD_Save ($currentMethodPath_t)
End if 

// Only increment if there was no error (in case I want to refer back to where I failed).
If ($error_l=0)
$currentMethod_l:=$currentMethod_l+1
End if 

End while 

// Close the progress window if it was open.
PROG_Close ($progID_l)
End if 

$0:=$error_l
