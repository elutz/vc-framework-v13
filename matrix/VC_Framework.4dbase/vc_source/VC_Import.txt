//%attributes = {"lang":"en","invisible":true,"shared":true,"executedOnServer":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Lutz Epperlein
// Date and time: 24.05.2013, 17:56:33
// ----------------------------------------------------
// Description:
//   Import outside created or changed methods 
//   existing on file system.
//
//   Note: executes on server.
//
//   Called by Macro.
// --[HEADEREND]---------------------------------------
C_LONGINT($error_l)

C_BOOLEAN(<>VC_Enabled_f)

UTIL_ERR_Startup 


If (<>VC_Enabled_f)

C_LONGINT($progID_l;$progThreshold_l)
$progThreshold_l:=1000
$progID_l:=PROG_Create ("Import";4;$progThreshold_l)


PROG_Update ($progID_l;0)
// Listing the content of the filesystem dir recursively
// accessing the timestamp of the files
C_TEXT($sourcepath)
$sourcepath:=Convert path POSIX to system(<>VC_SourcerootPOSIX_t+"/")
ARRAY TEXT($doclist;0)
DOCUMENT LIST($sourcepath;$doclist;Recursive parsing +Absolute path )

PROG_Update ($progID_l;1/4)

// Get the next to last method stamp.
C_LONGINT($currentMethodStamp_l;$lastMethodStamp_l)
$lastMethodStamp_l:=<>VC_LastMethodStamp_l

// Since the timespamp got from METHOD GET CODE is rather a counter of changes we have to 
// detect the real date and time.
ARRAY TEXT($methodslastChanged_at;0)

Repeat 
$currentMethodStamp_l:=$lastMethodStamp_l-1
METHOD GET PATHS(Path All objects ;$methodslastChanged_at;$currentMethodStamp_l;*)
$lastMethodStamp_l:=$lastMethodStamp_l-1
Until (Size of array($methodslastChanged_at)>0)

PROG_Update ($progID_l;2/4)

// find the most recent file 
C_BOOLEAN($locked;$hidden)
C_DATE($createdOn;$changedOn;$lastChangedOn)
C_TIME($createdAt;$changedAt;$lastChangedAt)
C_TEXT($recentfile)
$recentfile:=VC_UTIL_DOAPathToSystem ($methodslastChanged_at{1})
// and the get date and time of the last change
GET DOCUMENT PROPERTIES($recentfile;$locked;$hidden;$createdOn;$createdAt;$lastChangedOn;$lastChangedAt)

C_LONGINT($index)
For ($index;Size of array($doclist);1;-1)

GET DOCUMENT PROPERTIES($doclist{$index};$locked;$hidden;$createdOn;$createdAt;$changedOn;$changedAt)
// all files with newer timestamps then our last method time stamp
// will be imported
// remove older entries
If ($changedOn<$lastChangedOn)
DELETE FROM ARRAY($doclist;$index)

Else 

If (($changedOn=$lastChangedOn) & ($changedAt<$lastChangedAt))
DELETE FROM ARRAY($doclist;$index)

Else 
// hidden files (beginning with "." like .hgtags) must not be processed
If (Position(".";Replace string($doclist{$index};UTIL_FILE_GetParent ($doclist{$index});""))=1)
DELETE FROM ARRAY($doclist;$index)
End if 

End if 
End if 

End for 

ALERT("Import von "+String(Size of array($doclist))+" Methoden\n"+\
"neuer als "+String($lastChangedOn)+" "+String($lastChangedAt))

PROG_Update ($progID_l;3/4)

$error_l:=Choose(Size of array($doclist)>0;0;20000)

// And now import the code
C_LONGINT($progIDFiles_l)
$progIDFiles_l:=PROG_Create ("Files";Size of array($doclist);$progThreshold_l)

For ($index;1;Size of array($doclist))

// load the code from disk
C_TIME($docRef_h)
UTIL_ERR_HandlerInstall 
$docRef_h:=Open document($doclist{$index};Read Mode )
$error_l:=UTIL_ERR_HandlerRemove 

PROG_Update ($progIDFiles_l;$index/Size of array($doclist);$index)


If ($error_l=0)

C_BLOB($methodBody_b;$tempBody_b)
SET BLOB SIZE($methodBody_b;0)
SET BLOB SIZE($tempBody_b;0)
RECEIVE PACKET($docRef_h;$methodBody_b;Get document size($doclist{$index}))
CLOSE DOCUMENT($docRef_h)

// remove the BOM
COPY BLOB($methodBody_b;$tempBody_b;3;0;BLOB size($methodBody_b)-3)

// convert to text
$methodBody:=Convert to text($tempBody_b;"UTF-8")

$error_l:=Choose(Length($methodBody)=0;20001;0)
// fix line endings
C_TEXT($methodBody)
$methodBody:=Replace string($methodBody;Char(Line feed );Char(Carriage return ))

// system path to method path
C_TEXT($filePathPosix;$DOAPath_t)
$filePathPosix:=Convert path system to POSIX($doclist{$index};*)
$DOAPath_t:=Replace string($filePathPosix;<>VC_SourcerootPOSIX_t+"/";"")
$DOAPath_t:=Replace string($DOAPath_t;".txt";"")

// load the code
UTIL_ERR_HandlerInstall 
METHOD SET CODE($DOAPath_t;$methodBody;*)
$error_l:=UTIL_ERR_HandlerRemove 

End if 

End for 

PROG_Update ($progID_l;1)

Else 
// This should be impossible, because you have to open
// a method in order to run the the macro that calls
// this method...opening a method launches the component.
$error_l:=<>ERR_VC_NotEnabled_l
UTIL_ERR_ErrorMethod_t:=Current method name
End if 

If ($error_l#0)
UTIL_ERR_HandleError ($error_l;<>VC_ComponentName_t;UTIL_ERR_ErrorMethod_t)
End if 

PROG_CloseAll 